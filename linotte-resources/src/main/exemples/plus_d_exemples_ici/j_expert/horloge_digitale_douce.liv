/*http://langagelinotte.free.fr/punbb/viewtopic.php?id=311*/
globale
	lumière douce est un texte valant "20 20 20"
	lumière clair est un texte valant "255 255 255"
	décallage est un nombre valant -95
	deltah est un nombre valant ecranh / 2 - 400
	deltav est un nombre valant ecranv / 2 - 200
	élément v est un polygone, couleur = lumière douce, plein = vrai, x = 0, y = 0,     			dx1 = 10 ,dy1 = -10, dx2 = 20 ,dy2 = 0,     			dx3 = 20 ,dy3 = 40, dx4 = 10 ,dy4 = 50,     			dx5 = 0 ,dy5 = 40
	élément h est un polygone, couleur = lumière douce, plein = vrai, x = 0, y = 0,     			dx1 = 10 ,dy1 = -10, dx2 = 38 ,dy2 = -10,     			dx3 = 48 ,dy3 = 0, dx4 = 38 ,dy4 = 10,     			dx5 = 10 ,dy5 = 10
	chiffre1 est un casier de polygone
	chiffre2 est un casier de polygone
	chiffre3 est un casier de polygone
	chiffre4 est un casier de polygone
	chiffre5 est un casier de polygone
	chiffre6 est un casier de polygone
	rond1 est un cercle, couleur = lumière douce,  x = deltav + 285 + décallage, y = deltah + 230, rayon = 20, plein = vrai
	rond2 est un cercle, couleur = lumière douce,  x = deltav + 285 + décallage, y = deltah + 290, rayon = 20, plein = vrai
	rond3 est un cercle, couleur = lumière douce,  x = deltav + 506 + décallage, y = deltah + 230, rayon = 20, plein = vrai
	rond4 est un cercle, couleur = lumière douce,  x = deltav + 506 + décallage, y = deltah + 290, rayon = 20, plein = vrai
	vitre est une toile, couleur = "noir", pleinécran prend vrai, bordure = faux, largeur prend ecranv, hauteur prend ecranh, pointeur prend "main"
	patron0 est un casier de nombre valant 1, 0, 1, 1, 1, 1, 1, 1, 1, 1, 1
	patron1 est un casier de nombre valant 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1
	patron2 est un casier de nombre valant 1, 1, 1, 0, 0, 1, 1, 1, 1, 0, 0
	patron3 est un casier de nombre valant 1, 1, 1, 0, 0, 0, 0, 1, 1, 1, 1
	patron4 est un casier de nombre valant 0, 1, 0, 1, 1, 0, 0, 1, 1, 1, 1
	patron5 est un casier de nombre valant 1, 1, 1, 1, 1, 0, 0, 0, 0, 1, 1
	patron6 est un casier de nombre valant 1, 1, 1, 1, 1, 1, 1, 0, 0, 1, 1
	patron7 est un casier de nombre valant 1, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1
	patron8 est un casier de nombre valant 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1
	patron9 est un casier de nombre valant 1, 1, 1, 1, 1, 0, 0, 1, 1, 1, 1
	nombres est un casier de casier valant patron0, patron1, patron2, patron3, patron4, patron5, patron6, patron7, patron8, patron9

code :
	position 1 est un nombre valant deltav + 0 + décallage
	position 2 est un nombre valant deltav + 80 + décallage
	position 3 est un nombre valant deltav + 220 + décallage
	position 4 est un nombre valant deltav + 300 + décallage
	position 5 est un nombre valant deltav + 440 + décallage
	position 6 est un nombre valant deltav + 520 + décallage
	h & m & s est un nombre
	efface toile
	projette vitre
	projette rond1 & rond2 & rond3 & rond4
	parcours init avec position 1, chiffre1
	parcours init avec position 2, chiffre2
	parcours init avec position 3, chiffre3
	parcours init avec position 4, chiffre4
	parcours init avec position 5, chiffre5
	parcours init avec position 6, chiffre6
	vide touche
	tant que vrai
		h prend heure
		m prend minute
		s prend seconde
		parcours analyse avec h, chiffre1, chiffre2
		parcours analyse avec m, chiffre3, chiffre4
		parcours analyse avec s, chiffre5, chiffre6
		si s mod 2 == 0
			appelle On Rond avec rond1
			appelle On Rond avec rond2
			appelle On Rond avec rond3
			appelle On Rond avec rond4
		ferme
		sinon
			appelle Off Rond avec rond1
			appelle Off Rond avec rond2
			appelle Off Rond avec rond3
			appelle Off Rond avec rond4
		ferme
		attends 1 seconde
		si touche == "clique"
			termine
		ferme
	ferme

init :
	* d est un nombre
	* c est un casier de polygone
	parcours affiche avec #élément h, 111 + d, deltah + 139, c
	parcours affiche avec #élément h, 111 + d, deltah + 261, c
	parcours affiche avec #élément h, 111 + d, deltah + 383, c
	parcours affiche avec #élément v, 100 + d, deltah + 150, c
	parcours affiche avec #élément v, 100 + d, deltah + 210, c
	parcours affiche avec #élément v, 100 + d, deltah + 272, c
	parcours affiche avec #élément v, 100 + d, deltah + 332, c
	parcours affiche avec #élément v, 150 + d, deltah + 150, c
	parcours affiche avec #élément v, 150 + d, deltah + 210, c
	parcours affiche avec #élément v, 150 + d, deltah + 272, c
	parcours affiche avec #élément v, 150 + d, deltah + 332, c
	reviens

analyse :
	* n est un nombre
	* c1 est un casier de polygone
	* c2 est un casier de polygone
	parcours colorise avec nombres{entier(n/10)}, c1
	parcours colorise avec nombres{décimal(n/10)*10}, c2
	reviens

affiche :
	*temporaire est un polygone
	*x est un nombre
	*y est un nombre
	*casier est un casier de polygone
	projette temporaire
	ajoute temporaire dans casier
	appelle animation avec temporaire, x, y
	reviens

animation :
	*temporaire est un polygone
	*x est un nombre
	*y est un nombre
	flag est un drapeau valant faux
	aléatoire est un nombre valant 20
	mélange aléatoire
	aléatoire = aléatoire + 5
	tant que flag == faux
		si (x@temporaire) != x, x@temporaire = x@temporaire  + 1
		si (y@temporaire) != y, y@temporaire = y@temporaire  + 1
		si (x@temporaire) == x
			si (y@temporaire) == y, flag = vrai
		ferme
		attends aléatoire millisecondes
		tourne à droite temporaire de 1
	ferme
	tant que (angle@temporaire) mod 180 != 0
		tourne à droite temporaire de 1
		attends aléatoire millisecondes
	ferme
	reviens

colorise :
	* modèle est un casier de nombre
	* chiffre est un casier de polygone
	position est un nombre valant 0
	pour chaque modèle
		si joker == 1, appelle on avec chiffre{position}
		sinon appelle off avec chiffre{position}
		position = position + 1
	ferme
	reviens

on :
	* élément est un polygone
	si (couleur@élément) == lumière clair, reviens
	De 20 à 255
		couleur@élément = joker + " " + joker + " " + joker
		Attends 1 millisecondes
	Ferme
	reviens

off :
	* élément est un polygone
	si (couleur@élément) == lumière douce, reviens
	De 255 à 20
		couleur@élément = joker + " " + joker + " " + joker
		Attends 2 millisecondes
	Ferme
	reviens

on Rond :
	* élément est un cercle
	si (couleur@élément) == lumière clair, reviens
	De 20 à 255
		couleur@élément = joker + " " + joker + " " + joker
		Attends 2 millisecondes
	Ferme
	reviens

off Rond :
	* élément est un cercle
	si (couleur@élément) == lumière douce, reviens
	De 255 à 20
		couleur@élément = joker + " " + joker + " " + joker
		Attends 2 millisecondes
	Ferme
	reviens
