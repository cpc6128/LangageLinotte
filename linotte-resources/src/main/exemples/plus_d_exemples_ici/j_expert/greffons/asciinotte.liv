espèces
	lettre est un texte
	espèce box hérite de rectangle et contient lettre
globale
	info est un graffiti, x prend 233, y prend 540, couleur prend "bleu turquin"
	infoc est un graffiti, x prend 170, y prend 562, couleur prend "rouge", texte prend ""
	couleur off est un texte valant "noir"
	couleur on est un texte valant "menthe à l'eau"
	toile est une toile
	police est un texte
	lettres est un casier de graffiti
	form est un formulaire, titre prend "Le sélecteur de polices", largeur prend 400, hauteur prend 100
	s1 est un sélecteur, valeurs prend "", x prend 20, y prend 50
	l1 est un étiquette, texte prend "Choisissez la police:", x prend 20, y prend 20
	presse  est un pressepapier
	aide est un parchemin, texte prend "Asciinotte 1.0

		Survolez un caractère pour dévoiler sa valeur ASCII :
		- Un clic gauche sur le caractère copie la lettre dans le presse-papier.
		- Un clic droit sur le caractère copie la valeur ASCII dans le presse-papier.

		- Utilisez le sélecteur pour changer de police d'affichage.", x prend 50, y prend 350, largeur prend 500, taille prend 2, couleur prend "capucine"

principale :
	k & i est un nombre
	tmp est un texte
	efface toile
	pour k de 0 à 10
		pour i de 0 à 24
			si (i  + k*25) <= 255
				g est un graffiti
				r est un box
				fais réagir r à "souris entrante" pour afficher ascii
				fais réagir r à "souris sortante" pour assombrir
				fais réagir r à "clic souris" pour copier la lettre
				fais réagir r à "clic droit souris" pour copier ascii
				y@g prend 20 + (k * 30)
				x@g prend 25 + (i * 22)
				taille@g prend 12
				texte@g prend chr (i  + k*25)
				position@g prend 2
				couleur@g prend "argile"
				ajoute g dans lettres
				y@r prend 5 + k * 30
				x@r prend  25 + i * 22
				hauteur@r prend 21
				largeur@r prend 21
				couleur@r prend couleur off
				plein@r prend vrai
				position@r prend 1
				lettre@r prend chr (i  + k*25)
				projette r & g
			ferme
		ferme
	ferme
	projette info & infoc & aide & toile
	ajoute s1& l1 dans form
	fais réagir s1 à "clic souris" pour changer la police
	fais réagir form à "clic souris" pour fermer la fenêtre
	couleur@toile prend "noir"
		/*Construction de la liste des polices :*/
	pour chaque polices
		tmp prend tmp + joker + "|"
	ferme
	valeurs@s1 prend tmp
		/*Boucle infinie*/
	tant que vrai
		temporise
		vide touches
	ferme

afficher ascii :
	* b est un box
	texte@info prend lettre@b + ", valeur ascii : " + ascii (lettre@b)
	couleur@b prend couleur on
	transparence@b prend 100
	reviens

assombrir :
	* b est un box
	attends .2 seconde
	de 100 à 0
		transparence@b prend joker
		attends 10 millisecondes
	ferme
	reviens

changer la police :
	*s est un sélecteur
	police prend valeur@s
	pour chaque lettres
		police@joker prend police
	ferme
	Reviens

fermer la fenêtre :
	*f est un formulaire
	Termine

copier la lettre :
	*f est un box
	texte est un texte
	presse.copierhtml("<font face='" + police + "'>" + lettre@f + "</font>")
	texte prend "Le caractère '" + lettre@f + "' est copié dans le presse-papier !"
	texte@infoc prend texte
	attends 5 secondes
	si (texte@infoc) == texte, vide texte@infoc
	reviens

copier ascii :
	*f est un box
	texte est un texte
	presse.copier(ascii(lettre@f))
	texte prend "Le numéro ASCII '" + ascii(lettre@f) + "' est copié dans le presse-papier !"
	texte@infoc prend texte
	attends 5 secondes
	si (texte@infoc) == texte, vide texte@infoc
	reviens
