// auteur: $imon

// La notice se trouve dans le fichier AIDE.TXT
globale
	fond est une toile , hauteur prend 600 , largeur prend 600 , couleur prend "amande"
	casier est un casier de rectangle
	m est un nombre
	n est un nombre
	t est un texte
	pointeur est un point
	ARRET est un nombre valant -1
	compteur est un nombre valant 0
	graffiti1 est un graffiti ,  x prend 50 , y prend 25 , taille prend 10 , couleur prend "blanc" , texte prend " "

simulation :
		Essaie
			explore "ouvrir AIDE.TXT"
		Ferme
		Sinon
			Affiche "Impossible de trouver le fichier AIDE.TXT"
		Ferme
		efface tableau
		efface toile
		vide casier
		projette fond
		projette graffiti1 & pointeur
		m = 50
		n = 50
		pour chaque 2500
			carre est un rectangle
			ajoute carre dans casier
			x de carre = m
			y de carre = n
			largeur de carre = 9
			hauteur de carre = 9
			couleur de carre = "étain oxydé"
			plein de carre = vrai
			projette carre
			n = n + 10
			si n > 549
				n = 50
				m = m + 10
			ferme
		ferme
		tant que vrai
			ajoute ". en attente" dans texte de graffiti1
			vide t
			tant que t != "entrée"
				vide touche
				temporise
				x de pointeur = sourisx
				y de pointeur = sourisy
				t = touche
				si t == "clique"
					pour chaque casier
						si pointeur.collision(joker)
							si (couleur@joker) == "étain oxydé", copie "gris de payne" dans couleur de joker
							sinon copie "étain oxydé" dans couleur de joker
						ferme
					ferme
				ferme
				si t == "bas"
					pour chaque casier , copie "étain oxydé" dans couleur de joker
				ferme
				si t == "haut"
					pour chaque casier , copie "gris de payne" dans couleur de joker
				ferme
				si t == "droite", copie ":" dans t
				si t == ":"
					pour chaque casier 
						n = 40
						mélange n
						si n < 7, copie "gris de payne" dans couleur de joker
						sinon copie "étain oxydé" dans couleur de joker
					ferme
				ferme
				si t == "gauche", copie "!" dans t
				si t == "!"
					pour chaque casier
						si (couleur de joker) == "étain oxydé", copie "gris de payne" dans couleur de joker
						sinon si (couleur de joker) == "gris de payne", copie "étain oxydé" dans couleur de joker
					ferme
				ferme
				si t == "0"
					copie 0 dans compteur
					copie " génération 0. en attente" dans texte de graffiti1
					rafraîchis
				ferme
			ferme
			si compteur== 0, questionne ARRET sur "combien de générations ?"
			vide t
			tant que t != "entrée"
				ajoute 1 dans compteur
				concatène "génération " , compteur dans texte de graffiti1
				rafraîchis
				t = touche
				vide touche
				n = 0
				pour chaque casier
					m = 0
					parcours analyse avec ( n - 51 )
					parcours analyse avec ( n - 50 )
					parcours analyse avec ( n - 49 )
					parcours analyse avec ( n - 1 )
					parcours analyse avec ( n + 1 )
					parcours analyse avec ( n + 49 )
					parcours analyse avec ( n + 50 )
					parcours analyse avec ( n + 51 )
					si (couleur de joker) == "gris de payne"
						si m < 2, copie "gris perle" dans couleur de joker
						si m > 3, copie "gris perle" dans couleur de joker
					ferme
					si (couleur de joker) == "étain oxydé"
						si m == 3, copie "gris fer" dans couleur de joker
					ferme
					ajoute 1 dans n
				ferme
				pour chaque casier
					si (couleur de joker) == "gris fer", copie "gris de payne" dans couleur de joker
					si (couleur de joker) == "gris perle", copie "étain oxydé" dans couleur de joker
				ferme
				si compteur == ARRET, copie "entrée" dans t
			ferme
		ferme
		termine

analyse :
	*position est un nombre
		si position < 0, reviens
		si position > 2499, reviens
		si (couleur de ( casier { position } )) == "gris perle", ajoute 1 dans m
		sinon  si (couleur de  ( casier { position } )) == "gris de payne", ajoute 1 dans m
		reviens