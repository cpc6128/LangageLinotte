livre : le jeu de la vie
("  spécification: linotte 0.6.3  ")
("  auteur: $imon  ")


("La notice se trouve dans le fichier AIDE.TXT")
	les grands rôles :
		fond est une toile , hauteur vaut 600 , largeur vaut 600 , couleur vaut "amande"
		casier est un casier de rectangle
		m est un nombre
		n est un nombre
		t est un texte
		pointeur est un point
		ARRET est un nombre valant -1
		compteur est un nombre valant 0
		graffiti1 est un graffiti ,  x vaut 50 , y vaut 25 , taille vaut 10 , couleur vaut "blanc" , texte vaut " "

paragraphe : simulation
	les actions :
		Essaie, lis
			explore "ouvrir AIDE.TXT"
		Ferme
		Sinon, lis
			Affiche "Impossible de trouver le fichier AIDE.TXT"
		Ferme
		efface le tableau
		efface la toile
		vide casier
		projette le fond
		projette graffiti1
		m = 50
		n = 50
		pour chaque 2500 , lis
			concatène "carre" + joker dans t
			crée t un acteur rectangle
			ajoute création dans casier
			x de création = m
			y de création = n
			largeur de création = 9
			hauteur de création = 9
			couleur de création = "étain oxydé"
			plein de création = "oui"
			projette <<t>>
			n = n + 10
			si n est plus grand que 549 alors lis
				n = 50
				m = m + 10
			ferme
		ferme
		tant que 1 est égal à 1 , lis
			ajoute ". en attente" dans texte de graffiti1
			vide t
			tant que t est différent de "entrée" , lis
				vide touche
				temporise
				x de pointeur = sourisx
				y de pointeur = sourisy
				t = touche
				si t est égal à "clique" alors lis
					pour chaque casier , lis
						si pointeur est en collision avec joker alors lis
							si couleur de joker est égal à "étain oxydé" alors copie "gris de payne" dans couleur de joker
							sinon , copie "étain oxydé" dans couleur de joker
						ferme
					ferme
				ferme
				si t est égal à "bas" alors lis
					pour chaque casier , copie "étain oxydé" dans couleur de joker
				ferme
				si t est égal à "haut" alors lis
					pour chaque casier , copie "gris de payne" dans couleur de joker
				ferme
				si t est égal à "droite" alors copie ":" dans t
				si t est égal à ":" alors lis
					pour chaque casier , lis
						n = 40
						mélange n
						si n est plus petit que 7 alors copie "gris de payne" dans couleur de joker
						sinon , copie "étain oxydé" dans couleur de joker
					ferme
				ferme
				si t est égal à "gauche" alors copie "!" dans t
				si t est égal à "!" alors lis
					pour chaque casier , lis
						si couleur de joker est égal à "étain oxydé" alors copie "gris de payne" dans couleur de joker
						sinon , si couleur de joker est égal à "gris de payne" alors copie "étain oxydé" dans couleur de joker
					ferme
				ferme
				si t est égal à "0" alors lis
					copie 0 dans compteur
					copie " génération 0. en attente" dans texte de graffiti1
					rafraîchis
				ferme
			ferme
			si compteur est égal à 0 alors questionne ARRET sur "combien de générations ?"
			vide t
			tant que t est différent de "entrée" , lis
				ajoute 1 dans compteur
				concatène "génération " , compteur dans texte de graffiti1
				rafraîchis
				t = touche
				vide touche
				n = 0
				pour chaque casier , lis
					m = 0
					parcours analyse avec ( n - 51 )
					parcours analyse avec ( n - 50 )
					parcours analyse avec ( n - 49 )
					parcours analyse avec ( n - 1 )
					parcours analyse avec ( n + 1 )
					parcours analyse avec ( n + 49 )
					parcours analyse avec ( n + 50 )
					parcours analyse avec ( n + 51 )
					si couleur de joker est égal à "gris de payne" alors lis
						si m est plus petit que 2 alors copie "gris perle" dans couleur de joker
						si m est plus grand que 3 alors copie "gris perle" dans couleur de joker
					ferme
					si couleur de joker est égal à "étain oxydé" alors lis
						si m est égal à 3 alors copie "gris fer" dans couleur de joker
					ferme
					ajoute 1 dans n
				ferme
				pour chaque casier , lis
					si couleur de joker est égal à "gris fer" alors copie "gris de payne" dans couleur de joker
					si couleur de joker est égal à "gris perle" alors copie "étain oxydé" dans couleur de joker
				ferme
				si compteur est égal à ARRET alors copie "entrée" dans t
			ferme
		ferme
		termine

paragraphe : analyse
	les rôles :
		*position est un nombre
	les actions :
		si position est plus petit que 0 alors reviens
		si position est plus grand que 2499 alors reviens
		si couleur de ( casier { position } ) est égal à "gris perle" alors ajoute 1 dans m
		sinon , si couleur de  ( casier { position } ) est égal à "gris de payne" alors ajoute 1 dans m
		reviens

