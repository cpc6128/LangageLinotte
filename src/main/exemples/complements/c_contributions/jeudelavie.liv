// Concours SiteDuZero : http://fr.openclassrooms.com/forum/sujet/atelier-jeu-de-la-vie-de-conway?page=5#message-84593360
espèces
	santé valant "non"
	mental est un nombre
	espèce cellule hérite de rectangle et contient santé, mental

principale :
	// Les états :
	laisser vivre valant 90
	à tuer valant 33
	rien faire valant 66
	population est un casier de cellule
	zoom valant 1.5
	sol est une toile, largeur prend 500*zoom, hauteur prend 500*zoom, couleur prend "noir"
	eve est un cellule, couleur prend "rouge", largeur prend 8*zoom, hauteur prend 8*zoom, visible prend "oui", transparence prend 100, plein prend "non"
	début
		pour chaque 2500, lis
			ajoute clone(eve) dans population
			si (hasard(10)) = 0, lis
				santé de population{joker} prend "oui"
				plein de population{joker} prend "oui"
			ferme
			mental de population{joker} prend hasard(40)
			//si (mental de (population{joker})) > 35, couleur de (population{joker}) prend "jaune banane"
			si (mental de population{joker}) < 2, couleur de population{joker} prend "smaragdin"
			//couleur de (population{joker}) prend couleurs{10*(mental de (population{joker}))} // "feu vif"
			déplace population{joker} vers (joker mod 50)*10*zoom et entier(joker/50)*10*zoom
		ferme
		tant que vrai, lis
			pour chaque 2500, lis
				algo est un nombre
				position valant joker
				pour chaque {-51,-50,-49,-1,1,49,50,51}, algo prend ter(((position+joker))>=0 et ((position+joker)<2500),algo + ter((santé de population{position+joker})="oui",1,0),algo)
				si algo = 3, angle de population{joker} prend laisser vivre
				sinon si algo = 2, angle de population{joker} prend rien faire
				sinon si (algo < (mental de population{joker})) ou (algo > 3), angle de population{joker} prend à tuer
			ferme
			pour chaque population, lis
				santé de joker prend ter((angle de joker) = à tuer,"non",ter((angle de joker) = laisser vivre,"oui",santé de joker))
			  plein de joker prend ter((angle de joker) = à tuer,"non",ter((angle de joker) = laisser vivre,"oui",plein de joker))
				si (angle de joker) = à tuer, transparence de joker prend mental de joker / 2
				sinon transparence de joker prend (angle de joker)/2 + (mental de joker)*2
			ferme
			attends 10 millisecondes
		ferme