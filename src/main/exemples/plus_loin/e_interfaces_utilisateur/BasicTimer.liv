/*Basic-Timer est un minuteur simple.*/
globale
	form est un formulaire, titre prend "Basic-Timer", largeur prend 220, hauteur prend 150, image prend "medias/wikipedia_220px-Organic_mountain_grown_tea_leaf.jpg"
	l1 est un étiquette, texte prend "Choisissez le temps (en minute) :", x prend 20, y prend 20
	s1 est un sélecteur, valeurs prend "1|2|3|4|8|10|20", x prend 20, y prend 40, valeur prend 1
	b1 est un bouton, texte prend "Lancer !", x prend 100, y prend 60
	textes est un casier de texte valant "C'est prêt , Sir !", "Le temps est écoulé , monseigneur !", "On se réveille !"
	max est un nombre

formulaire :
	/*Construction de la fenêtre :*/
	Ajoute l1 & s1 & b1 dans form
	/*Initialisation des acteurs :*/
	Mesure textes dans max
	/*Ajout des évènements :*/
	Fais réagir b1 à "clic souris" pour minuteur
	Fais réagir form à  "clic souris" pour fermer fenêtre
	/*Boucle infinie, on attend les évènements invoqués par l'utilisateur.*/
	Tant que vrai
		temporise
		vide touches
	Ferme

Minuteur :
	temps est un nombre
	/*On calcule le temps en secondes :*/
	convertis valeur@s1 en temps
	temps prend temps * 60
	/*On cache la fenêtre le temps de l'attente :*/
	visible@form prend "non"
	/*Minuterie :*/
	Attends temps secondes
	/*L'attente est terminée, on prévient avec un son et...*/
	Joue "http://www.arobase.org/sons/sons/bell.wav"
	textes {aléatoire(max)} !
	/*On réaffiche la fenêtre :*/
	visible@form prend "oui"
	Reviens

Aléatoire :
	*n est un nombre
	r est un nombre
	r prend n
	Mélange r
	Retourne r

Fermer fenêtre :
	Termine
